import org.jooq.codegen.GenerationTool
import org.jooq.meta.jaxb.*

buildscript {
	dependencies {
		classpath(group: 'org.jooq', name: 'jooq-codegen', version: '3.16.4')
		classpath(group: 'org.jooq', name: 'jooq-meta-extensions', version: '3.16.4')
	}
}

plugins {
	id 'java'
	id("io.micronaut.library") version "3.2.2"
}

group 'com.optiply.infrastructure.data'
version '0.0.1-ALPHA'

sourceSets {
	main {
		java {
			srcDir("${projectDir}/build/generated/")
		}
	}

	test {
		java {
			srcDir("${projectDir}/build/generated/")
		}
	}
}

dependencies {
	annotationProcessor(group: 'org.projectlombok', name: 'lombok')
	annotationProcessor(group: 'io.micronaut.data', name: 'micronaut-data-processor')
	annotationProcessor(group: 'org.mapstruct', name: 'mapstruct-processor', version: '1.4.2.Final')
	implementation(group: 'org.jooq', name: 'jooq-meta', version: '3.16.4')
	implementation(group: 'org.jooq', name: 'jooq-codegen', version: '3.16.5')
	implementation(group: 'org.mapstruct', name: 'mapstruct', version: '1.4.2.Final')
	implementation(group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.2')
	implementation(group: 'io.micronaut.data', name: 'micronaut-data-r2dbc')
	implementation(group: 'io.micronaut.data', name: 'micronaut-data-jdbc')
	compileOnly(group: 'org.projectlombok', name: 'lombok')
	testImplementation(group: 'io.projectreactor', name: 'reactor-test', version: '3.4.16')
	runtimeOnly(group: 'io.micronaut.test', name: 'micronaut-test-spock', version: '3.1.1')
	//testImplementation(group: 'org.junit.jupiter', name: 'junit-jupiter-api')
	//testRuntimeOnly(group: 'org.junit.jupiter', name: 'junit-jupiter-engine')
	//testImplementation(group: 'org.junit.jupiter', name: 'junit-jupiter-api')
	//testImplementation(group: 'io.micronaut.test', name: 'micronaut-test-junit5')
	//testImplementation(group: 'org.mockito', name: 'mockito-core')
}

test {
	useJUnitPlatform()
}

task generateJooqSources {
	outputs.upToDateWhen { false }
	def mainDB = new Configuration()
			.withLogging(Logging.DEBUG)
			.withGenerator(new Generator()
					.withName("org.jooq.codegen.JavaGenerator")
					.withStrategy(new Strategy()
							.withName('org.jooq.codegen.DefaultGeneratorStrategy'))
					.withDatabase(new Database()
							.withName('org.jooq.meta.extensions.ddl.DDLDatabase')
							.withRecordTimestampFields(".*.*\\.UPDATED_AT")
							.withProperties(
									new Property()
											.withKey("scripts")
											.withValue("${projectDir}/build/resources/main/jooq_schema.sql"),
									new Property()
											.withKey("sort")
											.withValue("semantic")))
					.withGenerate(new Generate()
							.withPojos(true)
							.withDaos(false)
							.withInterfaces(false)
							.withFluentSetters(true)
							.withRelations(true)
							.withPojosEqualsAndHashCode(true))
					.withTarget(new Target()
							.withPackageName('com.optiply.infrastructure.data.models')
							.withDirectory("${projectDir}/build/generated/")))

	doLast {
		GenerationTool.generate(mainDB)
	}
}

compileJava.dependsOn(generateJooqSources)
generateJooqSources.dependsOn = [processResources, processTestResources]

java {
	modularity.inferModulePath = true
}
